-- | This module exposes the various events emitted during a mockchain run.
module Cooked.MockChain.Journal where

import Cooked.MockChain.Log
import Data.Map
import Data.Map qualified as Map
import PlutusLedgerApi.V3 qualified as Api

-- | This represents the writable elements that can be emitted throughout a
-- mockchain run.
data MockChainJournal where
  MockChainJournal ::
    { -- | Log entries generated by cooked-validators
      mcbLog :: [MockChainLogEntry],
      -- | Aliases stored by the user
      mcbAliases :: Map Api.BuiltinByteString String,
      -- | Notes taken by the user
      mcbNotes :: [String]
    } ->
    MockChainJournal

instance Semigroup MockChainJournal where
  MockChainJournal l a n <> MockChainJournal l' a' n' = MockChainJournal (l <> l') (a <> a') (n <> n')

instance Monoid MockChainJournal where
  mempty = MockChainJournal mempty mempty mempty

-- | Build a `MockChainJournal` from a single log entry
fromLogEntry :: MockChainLogEntry -> MockChainJournal
fromLogEntry entry = MockChainJournal [entry] mempty mempty

-- | Build a `MockChainJournal` from a single alias
fromAlias :: String -> Api.BuiltinByteString -> MockChainJournal
fromAlias s hash = MockChainJournal mempty (Map.singleton hash s) mempty

-- | Build a `MockChainJournal` from a single note
fromNote :: String -> MockChainJournal
fromNote s = MockChainJournal mempty mempty [show s]
